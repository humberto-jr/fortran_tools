MODULE wigner_3j_lib
!
      CONTAINS
      SUBROUTINE REC3JJ(THRCOF,L2,L3,M2,M3,L1MIN,L1MAX,LMATCH,NDIM,IER)
!
!  J1-RECURSION OF 3J-COEFFICIENTS
!
      IMPLICIT REAL * 8  (A-H)
      IMPLICIT REAL * 8  (O-Z)
      REAL * 8   L1, L2, L3, M1, M2, M3, L1MIN, L1MAX, NEWFAC, LMATCH
      DIMENSION THRCOF(NDIM)
!
      DATA  ZERO,EPS,HALF,ONE /0D0,0.0001D0,0.5D0,1D0/
      DATA TWO, THREE /2D0,3D0/
!
!  ROUTINE TO GENERATE SET OF 3J-COEFFICIENTS   L1  L2  L3
!                                               M1  M2  M3
!  BY RECURSION FROM L1MIN = MAX0(/L2-L3/,/M1/)
!                 TO L1MAX =       L2+L3
!
!  THE RESULTING 3J-COEFFICIENTS ARE STORED AS THRCOF(L1-L1MIN+1)
!
!  FOR A DISCUSSION OF THE RECURSION EQUATION USED SEE K. SCHULTEN
!  AND R.G. GORDON, J. MATH. PHYS. 16, 1961-1970 (1975), IBID. 16,
!  1971-1988 (1975)
!  FOR THE SAKE OF NUMERICAL STABILITY THE RECURSION WILL PROCEED
!  SIMULTANEOUSLY FORWARDS AND BACKWARDS, STARTING FROM L1MIN AND
!  L1MAX, RESPECTIVELY.
!  LMATCH IS THE L1-VALUE AT WHICH FORWARD AND BACKWARD RECURSION ARE
!  MATCHED.
!  NDIM IS THE LENGTH OF THE ARRAY THRCOF.
!
!  IER IS SET TO -1 IF L2-/M2/ OR L3-/M3/ LESS THAN ZERO OR NOT
!                   INTEGER, IN WHICH CASE ALL 3J-COEFFICIENTS
!                   VANISH.
!  IER IS SET TO -2 IF NUMBER OF POSSIBLE 3J-COEFFICIENTS EXCEEDS NDIM
!  IER IS SET TO NON-NEGATIVE NUMBER OTHERWISE (0 + TIMES OF RESCALING)
!
!  TINY SHOULD BE SET CLOSE TO THE SMALLEST POSITIVE FLOATING POINT
!  NUMBER WHICH IS REPRESENTABLE ON THE COMPUTER.  SRTINY IS SQUARE
!  ROOT OF TINY .
!
      DATA TINY, SRTINY  /1.0D-10,1.0D-05/
!
!  HUGE SHOULD BE SET CLOSE TO LARGEST POSITIVE FLOATING POINT
!  NUMBER WHICH IS REPRESENTABLE ON THE COMPUTER.  SRHUGE IS
!  SQUARE ROOT OF HUGE .
!
      DATA  HUGE, SRHUGE /1.0D10,1.0D05/
!
      LMATCH = ZERO
      M1 = - M2 - M3
!
!  CHECK RELATIVE MAGNITUDE OF L- AND M-VALUES
      IF(L2-DABS(M2)+EPS) 5,1,1
    1 IF(L3-DABS(M3)+EPS) 5,2,2
    2 IF(DMOD(L2+DABS(M2)+EPS,ONE).GE.EPS+EPS)   GO TO 5
      IF(DMOD(L3+DABS(M3)+EPS,ONE).GE.EPS+EPS)   GO TO 5
!
!
!
!  LIMITS FOR L1
!
      L1MIN = DMAX1(DABS(L2-L3),DABS(M1))
      L1MAX = L2 + L3
      IF(L1MIN.LT.L1MAX-EPS)   GO TO 20
      IF(L1MIN.LT.L1MAX+EPS)   GO TO 10
!
!
!  THIS IS REACHED IF L2-/M2/ AND L3-/M3/  LESS THAN ZERO OR NOT INTEGER
!
    5 IER = - 1
      WRITE(6,51) L2, L3, M1, M2, M3
   51 FORMAT(///1X,15H3J-COEFFICIENTS,9X,2HL1,2F7.1/20X,3F7.1,4X, &
      57HDO NOT SATISFY THE CONDITION L2-/M2/ AND L3-/M3/ GE ZERO, &
      11HAND INTEGER)
      RETURN
!
!
!  THIS IS REACHED IN CASE THAT L1 CAN TAKE ONLY ONE VALUE,
!  I.E. L1MIN = L1MAX
!
   10 IER = 0
      THRCOF(1) = (-ONE) ** IDINT(DABS(L2+M2-L3+M3)+EPS) / &
      DSQRT(L1MIN + L2 + L3 + ONE)
      L1CMIN = L1MIN
      L1CMAX = L1MAX
      RETURN
!
!
!
   20 IER = 0
      NFIN = IDINT(L1MAX-L1MIN+ONE+EPS)
      IF(NDIM-NFIN)  21, 23, 23
!
!  DIMENSION OF THRCOF NOT LARGE ENOUGH TO HOLD ALL THE COEFFICIENTS
!  REQUIRED
!
   21 IER = - 2
      WRITE(6,22)  L2, L3, M1, M2, M3, NFIN, NDIM
   22 FORMAT(///1X,15H3J-COEFFICIENTS,9X,2HL1,2F7.1/20X,3F7.1,4X, &
      26HEXCEED STORAGE PROVIDED  (,I4,1H,,I4,1H))
      RETURN
!
!
!  STARTING FORWARD RECURSION FROM L1MIN TAKING NSTEP1 STEPS
!
   23 L1 = L1MIN
      THRCOF(1) = SRTINY
      SUM1 = (L1+L1+ONE) * TINY
!
!
      LSTEP = 1
   30 LSTEP = LSTEP + 1
      L1 = L1 + ONE
!
!
      OLDFAC = NEWFAC
      A1 = (L1+L2+L3+ONE) * (L1-L2+L3) * (L1+L2-L3) * (-L1+L2+L3+ONE)
      A2 = (L1+M1) * (L1-M1)
      NEWFAC = DSQRT(A1*A2)
      IF(L1.LT.ONE+EPS)   GO TO 40
!
!
      DV = - L2*(L2+ONE) * M1 + L3*(L3+ONE) * M1 + L1*(L1-ONE) * (M3-M2)
      DENOM = (L1-ONE) * NEWFAC
!
      IF(LSTEP-2)  32, 32, 31
!
   31 C1OLD = DABS(C1)
   32 C1 = - (L1+L1-ONE) * DV / DENOM
      GO TO 50
!
!  IF L1 = 1  (L1-1) HAS TO BE FACTORED OUT OF DV, HENCE

   40 C1 = - (L1+L1-ONE) * L1 * (M3-M2) / NEWFAC
!
   50 IF(LSTEP.GT.2)   GO TO 60
!
!
!  IF L1 = L1MIN + 1  THE THIRD TERM IN THE RECURSION EQUATION VANISHES
!  , HENCE
      X = SRTINY * C1
      THRCOF(2) = X
      SUM1 = SUM1 + TINY * (L1+L1+ONE) * C1*C1
      IF(LSTEP.EQ.NFIN)   GO TO 220
      GO TO 30
!
!
   60 C2 = - L1 * OLDFAC / DENOM
!
!  RECURSION TO THE NEXT 3J-COEFFICIENT X
!
      X = C1 * THRCOF(LSTEP-1) + C2 * THRCOF(LSTEP-2)
      THRCOF(LSTEP) = X
      SUMFOR = SUM1
      SUM1 = SUM1 + (L1+L1+ONE) * X*X
      IF(LSTEP.EQ.NFIN)   GO TO 100
!
!  SEE IF LAST UNNORMALIZED 3J-COEFFICIENT EXCEEDS SRHUGE
!
      IF(DABS(X).LT.SRHUGE)   GO TO 80
!
!  THIS IS REACHED IF LAST 3J-COEFFICIENT LARGER THAN SRHUGE
!  SO THAT THE RECURSION SERIES THRCOF(1), ... , THRCOF(LSTEP)
!  HAS TO BE RESCALED TO PREVENT OVERFLOW
!
      IER = IER + 1
      DO 70 I=1,LSTEP
      IF(DABS(THRCOF(I)).LT.SRTINY)   THRCOF(I) = ZERO
   70 THRCOF(I) = THRCOF(I) / SRHUGE
      SUM1 = SUM1 / HUGE
      SUMFOR = SUMFOR / HUGE
      X = X / SRHUGE
!
!  AS LONG AS /C1/ IS DECREASING THE RECURSION PROCEEDS TOWARDS
!  INCREASING 3J-VALUES AND, HENCE, IS NUMERICALLY STABLE.  ONCE
!  AN INCREASE OF /C1/ IS DETECTED THE RECURSION DIRECTION IS
!  REVERSED.
!
   80 IF(C1OLD-DABS(C1))   100, 100, 30
!
!
!  KEEP THREE 3J-COEFFICIENTS AROUND LMATCH FOR COMPARISION WITH
!  BACKWARD RECURSION.
!
  100 LMATCH = L1 - 1
      X1 = X
      X2 = THRCOF(LSTEP-1)
      X3 = THRCOF(LSTEP-2)
      NSTEP2 = NFIN - LSTEP + 3
!
!
!
!
!  STARTING BACKWARD RECURSION FROM L1MAX TAKING NSTEP2 STEPS, SO
!  THAT FORWARD AND BACKWARD RECURSION OVERLAP AT THREE POINTS
!  L1 = LMATCH+1, LMATCH, LMATCH-1.
!
      NFINP1 = NFIN + 1
      NFINP2 = NFIN + 2
      NFINP3 = NFIN + 3
      L1 = L1MAX
      THRCOF(NFIN) = SRTINY
      SUM2 = TINY * (L1+L1+ONE)
!
      L1 = L1 + TWO
      LSTEP = 1
  110 LSTEP = LSTEP + 1
      L1 = L1 - ONE
!
      OLDFAC = NEWFAC
      A1S = (L1+L2+L3)*(L1-L2+L3-ONE)*(L1+L2-L3-ONE)*(-L1+L2+L3+TWO)
      A2S = (L1+M1-ONE) * (L1-M1-ONE)
      NEWFAC = DSQRT(A1S*A2S)
!
      DV = - L2*(L2+ONE) * M1 + L3*(L3+ONE) * M1 + L1*(L1-ONE) * (M3-M2)
!
      DENOM = L1 * NEWFAC
      C1 = - (L1+L1-ONE) * DV / DENOM
      IF(LSTEP.GT.2)   GO TO 120
!
!  IF L1 = L1MAX + 1  THE THIRD TERM IN THE RECURSION FORMULA VANISHES
!
      Y = SRTINY * C1
      THRCOF(NFIN-1) = Y
      SUMBAC = SUM2
      SUM2 = SUM2 + TINY * (L1+L1-THREE) * C1*C1
!
      GO TO 110
!
!
  120 C2 = - (L1 - ONE) * OLDFAC / DENOM
!
!  RECURSION TO THE NEXT 3J-COEFFICIENT Y
!
      Y = C1 * THRCOF(NFINP2-LSTEP) + C2 * THRCOF(NFINP3-LSTEP)
!
      IF(LSTEP.EQ.NSTEP2)   GO TO 200
!
      THRCOF(NFINP1-LSTEP) = Y
      SUMBAC = SUM2
      SUM2 = SUM2 + (L1+L1-THREE) * Y*Y
!
!  SEE IF LAST UNNORMALIZED 3J-COEFFICIENT EXCEEDS SRHUGE
!
      IF(DABS(Y).LT.SRHUGE)   GO TO 110
!
!  THIS IS REACHED IF LAST 3J-COEFFICIENT LARGER THAN SRHUGE
!  SO THAT THE RECURSION SERIES THRCOF(NFIN), ... ,THRCOF(NFIN-LSTEP+1)
!  HAS TO BE RESCALED TO PREVENT OVERFLOW
!
      IER = IER + 1
      DO 130 I=1,LSTEP
      INDEX = NFIN - I + 1
      IF(DABS(THRCOF(INDEX)).LT.SRTINY)   THRCOF(INDEX) = ZERO
  130 THRCOF(INDEX) = THRCOF(INDEX) / SRHUGE
      SUM2 = SUM2 / HUGE
      SUMBAC = SUMBAC / HUGE
!
!
      GO TO 110
!
!
!  THE FORWARD RECURSION 3J-COEFFICIENTS X1, X2, X3 ARE TO BE MATCHED
!  WITH THE CORRESPONDING BACKWARD RECURSION VALUES Y1, Y2, Y3.
!
  200 Y3 = Y
      Y2 = THRCOF(NFINP2-LSTEP)
      Y1 = THRCOF(NFINP3-LSTEP)
!
!
!  DETERMINE NOW RATIO SUCH THAT YI = RATIO * XI  (I=1,2,3) HOLDS
!  WITH MINIMAL ERROR.
!
      RATIO = ( X1*Y1 + X2*Y2 + X3*Y3 ) / ( X1*X1 + X2*X2 + X3*X3 )
      NLIM = NFIN - NSTEP2 + 1
!
      IF(DABS(RATIO).LT.ONE)   GO TO 211
!
      DO 210 N=1,NLIM
  210 THRCOF(N) = RATIO * THRCOF(N)
      SUMUNI = RATIO * RATIO * SUMFOR + SUMBAC
      GO TO 230
!
  211 NLIM = NLIM + 1
      RATIO = ONE / RATIO
      DO 212 N=NLIM,NFIN
  212 THRCOF(N) = RATIO * THRCOF(N)
      SUMUNI = SUMFOR + RATIO*RATIO*SUMBAC
      GO TO 230
!
  220 SUMUNI = SUM1
!
!
!  NORMALIZE 3J-COEFFICIENTS
!
  230 CNORM = ONE / DSQRT(SUMUNI)
!
!  SIGN CONVENTION FOR LAST 3J-COEFFICIENT DETERMINES OVERALL PHASE
!
      SIGN1 = DSIGN(ONE,THRCOF(NFIN))
      SIGN2 = (-ONE) ** IDINT(DABS(L2+M2-L3+M3)+EPS)
      IF(SIGN1*SIGN2) 235,235,236
  235 CNORM = - CNORM
!
  236 IF(DABS(CNORM).LT.ONE)   GO TO 250
!
      DO 240 N=1,NFIN
  240 THRCOF(N) = CNORM * THRCOF(N)
      RETURN
!
  250 THRESH = TINY / DABS(CNORM)
      DO 251 N=1,NFIN
      IF(DABS(THRCOF(N)).LT.THRESH)   THRCOF(N) = ZERO
  251 THRCOF(N) = CNORM * THRCOF(N)
!
      RETURN
      END SUBROUTINE REC3JJ
END MODULE wigner_3j_lib

